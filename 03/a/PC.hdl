// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //If Elseの実装方法分からなかったけど逆からやって上書きすればいいのか...、納得。
    Inc16(in=fb, out=inco);
    Mux16(a=fb, b=inco, sel=inc, out=out1);
    Mux16(a=out1, b=in, sel=load, out=out2);
    Mux16(a=out2, b[0..15]=false, sel=reset, out=out3);
    //もうロードするか否かは決まってるのでloadビットはtrue確定
    //ここでloadのinputに依存するようにするとresetが無駄になる
    Register(in=out3, load=true, out=out, out=fb);
}
